plugins {
    id 'kotlin-multiplatform'
    id 'kotlinx-serialization'
    id 'com.android.library'
    id("org.jetbrains.kotlin.native.cocoapods")
}

apply from: "${rootProject.rootDir}/gradle/android_mpp_commons.gradle"

apply from: rootProject.file('gradle/simple_publish.gradle')

android {
    sourceSets.each {
        it.manifest.srcFile "src/androidMain/AndroidManifest.xml"
    }
}

repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

apply plugin: 'org.jetbrains.dokka'

kotlin {
    android() {
        publishLibraryVariants("release")
    }
    jvm()

    iosX64("ios")

    if (project.findProperty("isItPublishing")?.toBoolean() == true) {
        iosArm32("iosArm32")
        iosArm64("iosArm64")
    }

    println(targets.names)

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.2.1'

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.11.0"

            }

            languageSettings {
//                enableLanguageFeature('InlineClasses')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
//                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.0"

                implementation "com.squareup.okhttp3:okhttp:3.14.1"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1'
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.0"

                implementation "com.squareup.okhttp3:okhttp:3.14.1"

            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1'
            }
        }
        iosMain {
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.2.1'

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.11.0"
            }
        }
        iosTest {
            dependencies {
                
            }
        }
        if (project.findProperty("isItPublishing")?.toBoolean() == true) {
            iosArm32Main.dependsOn iosMain
            iosArm32Test.dependsOn iosTest
            iosArm64Main.dependsOn iosMain
            iosArm64Test.dependsOn iosTest
        }
    }

    sourceSets.all {
        languageSettings {
            useExperimentalAnnotation("objc-generics")
            useExperimentalAnnotation("kotlin.ExperimentalMultiplatform")
            useExperimentalAnnotation("kotlin.Experimental")
            useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
            useExperimentalAnnotation("kotlin.contracts.ExperimentalContracts")
        }
    }

//    targets.all { target ->
//        def publication = publishing.publications.findByName(target.name)
//
//        if (publication != null) {
//            publication.artifact emptyJavadoc
//        }
//    }

    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "DomeHTTP is a Kotlin Multiplatform Module"
        homepage = "https://github.com/hazer/DomeHTTP"
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}


task iosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn kotlin.targets.ios.binaries.getExecutable('test', 'DEBUG').linkTaskName
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getExecutable('test', 'DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}